definition {

	macro addFragment {
		PageEditor.gotoTab(tabName = "Available");

		PageEditor.expandFragmentCollection(collectionName = "${collectionName}");

		Click(
			key_collectionName = "${collectionName}",
			key_fragmentName = "${fragmentName}",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_FRAGMENT");

		AssertElementPresent(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER");
	}

	macro editFragmentImage {
		MouseOver.mouseOverNotVisible(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER");

		Click(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD");

		SelectFrame(locator1 = "IFrame#DIALOG");

		AssertElementPresent(locator1 = "Button#SELECT_FILE");

		UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
			locator1 = "TextInput#FILE",
			value1 = "${fileName}");

		SelectFrameTop();

		Click(locator1 = "Button#ADD");
	}

	macro editFragmentText {
		MouseOver.mouseOverNotVisible(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD");

		Click(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD");

		if (IsElementPresent(locator1 = "Button#EDIT")) {
			Button.clickEdit();
		}

		Type(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD_CK_EDITOR",
			value1 = "${text}");

		KeyPress(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD_CK_EDITOR",
			value1 = "\SPACE");

		KeyPress(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD_CK_EDITOR",
			value1 = "\BACK_SPACE");
	}

	macro expandFragmentCollection {
		if (IsElementPresent(locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL_COLLAPSED", key_collectionName = "${collectionName}")) {
			AssertClick(
				key_collectionName = "${collectionName}",
				locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL_COLLAPSED",
				value1 = "${collectionName}");
		}

		AssertElementPresent(
			key_collectionName = "${collectionName}",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL_EXPANDED");
	}

	macro gotoTab {
		AssertClick(
			key_tabName = "${tabName}",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_TAB",
			value1 = "${tabName}");
	}

	macro moveFragment {
		MouseOver(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_BODY");

		if ("${direction}" == "up") {
			Click.clickNoWaitForVisible(
				key_fragmentName = "${fragmentName}",
				locator1 = "Fragment#FRAGMENT_HEADER_UP_ARROW_BUTTON");
		}
		else if ("${direction}" == "down") {
			Click.clickNoWaitForVisible(
				key_fragmentName = "${fragmentName}",
				locator1 = "Fragment#FRAGMENT_HEADER_DOWN_ARROW_BUTTON");
		}
	}

	macro removeFragment {
		MouseOver.mouseOverNotVisible(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER");

		Click(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER_DELETE_BUTTON");

		AssertElementNotPresent(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER");
	}

	macro viewAutoSaveMessage {
		AssertTextEquals.assertPartialText(
			locator1 = "PageEditor#AUTOSAVE_MESSAGE",
			value1 = "Changes saved at");
	}

	macro viewCollectionNotPresent {
		AssertElementNotPresent(
			key_collectionName = "${collectionName}",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL");
	}

	macro viewEditorEmpty {
		AssertElementPresent(locator1 = "PageEditor#EMPTY_MESSAGE");
	}

	macro viewFragment {
		AssertElementPresent(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER");
	}

	macro viewFragmentEditableText {
		AssertTextEquals(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD",
			value1 = "${editableText}");
	}

	macro viewFragmentOrder {
		AssertElementPresent(
			key_fragmentName = "${fragmentName}",
			key_position = "${position}",
			locator1 = "Fragment#FRAGMENT_HEADER_POSITION");
	}

}