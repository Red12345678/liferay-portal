@component-name = "portal-user-interface"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@priority = "5"
	test AddThemeSite {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "WesterosTheme#AddThemeSite";
		property theme.file.name = "westeros-bank-theme.war";
		var themeContainsResources = PropsUtil.get("theme.contains.resources");

		if ("${themeContainsResources}" == "true") {
			ProductMenu.gotoPortlet(
				category = "Sites",
				panel = "Control Panel",
				portlet = "Sites");

			Site.addSiteTemplateCP(
				siteName = "Westeros Bank Theme Site",
				siteTemplateName = "Westeros Bank Theme");

			Navigator.openSiteURL(siteName = "Westeros Bank Theme Site");

			AssertVisible(locator1 = "UserBar#USER_AVATAR_IMAGE");

			AssertClick(
				key_pageName = "For You",
				locator1 = "Home#PAGE",
				value1 = "FOR YOU");

			AssertTextEquals(
				key_title = "Always thinking of you",
				locator1 = "Header#H2_TITLE",
				value1 = "Always thinking of you");
		}
		else {
			var appName = "westeros-bank-theme";

			ProductMenu.gotoPortlet(
				category = "Apps",
				panel = "Control Panel",
				portlet = "App Manager");

			Type(
				locator1 = "TextInput#SEARCH",
				value1 = "${appName}");

			Click(locator1 = "Icon#BASIC_SEARCH");

			AssertVisible(
				key_itemName = "westeros-bank-theme",
				locator1 = "ListGroupItem#ITEM_NAME_LINK");
		}
	}

}